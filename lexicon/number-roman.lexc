! number.lexc
!
! Released under the terms of the MIT License
! Copyright (c) 2011-2015 Çağrı Çöltekin <cagri@coltekin.net>
! 
! Permission is hereby granted, free of charge, to any person obtaining a
! copy of this software and associated documentation files (the "Software"),
! to deal in the Software without restriction, including without limitation
! the rights to use, copy, modify, merge, publish, distribute, sublicense,
! and/or sell copies of the Software, and to permit persons to whom the
! Software is furnished to do so, subject to the following conditions:
! 
! The above copyright notice and this permission notice shall be included in
! all copies or substantial portions of the Software.
! 
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
! FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
! DEALINGS IN THE SOFTWARE.
!

!
! Roman numerals
!
! also a bit ugly to do it here, but it is more convenient.
! We just try to recognize them, no trickery to convert them.
!
! We only recognize the modern notation, but we 
! additionally accept IIII.
!

LEXICON Root
< [M|D|C|L|X|V|I]+ .o.          ! make sure there is at least one character
  M*                            ! thousands
  (C [D|M] | (D) C^{0,3})       ! hundreds
  (X [L|C] | (L) X^{0,3})       ! tens
  (I [V|X] | (V) I^{0,3} | I^4) >           #;
! the following is exactly the same, but with special UNICODE characters
< [Ⅿ|Ⅾ|Ⅽ|Ⅼ|Ⅹ|Ⅴ|Ⅰ]+ .o.
  Ⅿ*
  (Ⅽ [Ⅾ|Ⅿ] | (Ⅾ) Ⅽ^{0,3})
  (Ⅹ [Ⅼ|Ⅽ] | (Ⅼ) Ⅹ^{0,3})
  (Ⅰ [Ⅴ|Ⅹ] | (Ⅴ) Ⅰ^{0,3} | Ⅰ^4) >           #;
!
! and lowercase versions, 
!
! TODO: maybe we should do this in morph-phon part
< [m|d|c|l|x|v|i]+ .o.
  m*
  (c [d|m] | (d) c^{0,3})
  (x [l|c] | (l) x^{0,3})
  (i [v|x] | (v) i^{0,3} | i^4) >           #;
! the following is exactly the same, but with lowercase characters
< [ⅿ|ⅾ|ⅽ|ⅼ|ⅹ|ⅴ|ⅰ]+ .o.
  ⅿ*
  (ⅽ [ⅾ|ⅿ] | (ⅾ) ⅽ^{0,3})
  (ⅹ [ⅼ|ⅽ] | (ⅼ) ⅹ^{0,3})
  (ⅰ [ⅴ|ⅹ] | (ⅴ) ⅰ^{0,3} | ⅰ^4) >           #;

! combined unicode characters
< Ⅰ|Ⅱ|Ⅲ|Ⅳ|Ⅴ|Ⅵ|Ⅶ|Ⅷ|Ⅸ|Ⅹ|Ⅺ|Ⅻ >                 #;
< ⅰ|ⅱ|ⅲ|ⅳ|ⅴ|ⅵ|ⅶ|ⅷ|ⅸ|ⅹ|ⅺ|ⅻ >                 #;
